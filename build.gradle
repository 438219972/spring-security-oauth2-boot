buildscript {
	dependencies {
		classpath 'io.spring.gradle:spring-build-conventions:0.0.20.RELEASE'
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
	}
	repositories {
		maven { url 'https://plugins.gradle.org/m2/' }
		maven { url 'https://repo.spring.io/plugins-release' }
		maven { url 'https://repo.spring.io/plugins-milestone-local' }
	}
}
apply plugin: 'io.spring.convention.root'

allprojects { project ->
	project.repositories { repos ->
		all { repo ->
			if (!repo.url.toString().startsWith("https://repo.spring.io/libs")) {
				return;
			}
			if (!project.hasProperty("artifactoryUsername")) {
				return;
			}
			repo.credentials {
				username = artifactoryUsername
				password = artifactoryPassword
			}
		}
	}
}

group = 'org.springframework.security.oauth.boot'
description = 'Spring Security OAuth2 Auto Configuration'

ext.snapshotBuild = version.contains("SNAPSHOT")
ext.releaseBuild = !version.contains("-")
ext.milestoneBuild = !(snapshotBuild || releaseBuild)
